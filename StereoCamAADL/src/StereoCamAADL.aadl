package StreoCamAADL
public
	with Processors::ARM;

	with Buses::I2C;

	with Buses::USB;

	with Processor_Properties;

	with Memories;

	with StereoCamAADL_Threads;

	with StereoCamAADL_Devices;
	
	with StereoCamAADL_Data;

	with Buses::Ethernet;

	system vantSystem
	end vantSystem;

	system implementation vantSystem.with_devices
		subcomponents
			this_controller: process controller.withThreads;
			this_A57: processor A57.impl;
			this_camera_L: device StereoCamAADL_Devices::camera.impl;
			this_camera_R: device StereoCamAADL_Devices::camera.impl;
			this_laser: device StereoCamAADL_Devices::laser;
			this_gps: device StereoCamAADL_Devices::GPS;
			this_usb_1: bus Buses::USB::USB.impl;
			this_usb_2: bus Buses::USB::USB.impl;
			this_ethernet: bus Buses::Ethernet::Ethernet.impl;
			flash_memory: memory Memories::Flash_Memory.impl {Memory_size => 2 GByte;};
		connections
			bus_usb_1_A57: bus access this_usb_1 -> this_A57.usb_bus_1;
			bus_usb_2_A57: bus access this_usb_2 -> this_A57.usb_bus_2;
			bus_usb_3_A57: bus access this_ethernet -> this_A57.ethernet;
			bus_usb_1_cam_l: bus access this_usb_1 -> this_camera_l.usbPort;
			bus_usb_2_cam_r: bus access this_usb_2 -> this_camera_r.usbPort;
			bus_usb_3_laser: bus access this_ethernet -> this_laser.Serial_Wire;
			
			
			
			l_cam_trigger: port this_camera_l.camera_trigger -> this_controller.cameras_trigger;
			r_cam_trigger: port this_camera_r.camera_trigger -> this_controller.cameras_trigger;
			laser_trigger: port this_laser.proc_evt -> this_controller.laser_trigger;
			gps_trigger: port this_gps.gps_trigger -> this_controller.gps_trigger;
		properties
			Actual_Processor_Binding => (reference (this_A57)) applies to this_controller;
	end vantSystem.with_devices;

-----------------------
-------PROCESSOR-------
-----------------------
	processor A57 extends Processors::ARM::Generic_ARM
		features
			i2c_bus: requires bus access Buses::I2C::I2C.IMPL;
			usb_bus_1: requires bus access Buses::USB::USB.impl;
			usb_bus_2: requires bus access Buses::USB::USB.impl;
			ethernet: requires bus access Buses::Ethernet::Ethernet.impl;
		properties
			Processor_Properties::Processor_Frequency => 1430 Mhz;
			Scheduling_Protocol => (POSIX_1003_HIGHEST_PRIORITY_FIRST_PROTOCOL);
	end A57;

	processor implementation A57.impl
	end A57.impl;

-----------------------
-------PROCESS---------
-----------------------
	process controller
		features
			cameras_trigger: in event port;
			laser_trigger: in event port;
			gps_trigger: in event port;
			control_data: out data port StereoCamAADL_Data::control_data.impl;
	end controller;		

	process implementation controller.withThreads
		subcomponents
			data_saver_thread: thread StereoCamAADL_Threads::data_saver.impl;
			stereo_img_collector_thread: thread StereoCamAADL_Threads::stereo_img_collector.impl;
			laser_data_collector_thread: thread StereoCamAADL_Threads::laser_data_collector;
			gps_data_collector_thread: thread StereoCamAADL_Threads::gps_data_collector;
			wifi_control_data_collector_thread: thread StereoCamAADL_Threads::wifi_control_data_collector.impl;
			wifi_video_data_sender_thread: thread StereoCamAADL_Threads::wifi_video_data_sender.impl;
		connections
			cameras_trigger_conn: port cameras_trigger -> stereo_img_collector_thread.cameras_trigger;
			l_cam_img_save: port stereo_img_collector_thread.l_camera_img_out -> data_saver_thread.l_camera_img_in;
			r_cam_img_save: port stereo_img_collector_thread.r_camera_img_out -> data_saver_thread.r_camera_img_in;
			
			laser_trigger_conn: port laser_trigger -> laser_data_collector_thread.laser_trigger;
			laser_data: port laser_data_collector_thread.laser_data_out -> data_saver_thread.laser_data_in;
			
			gps_trigger_conn: port gps_trigger -> gps_data_collector_thread.gps_trigger;
			gps_data: port gps_data_collector_thread.gps_data_out -> data_saver_thread.gps_data_in;
			
			l_cam_img_stream: port stereo_img_collector_thread.l_camera_img_out -> wifi_video_data_sender_thread.l_camera_img_in;
			r_cam_img_stream: port stereo_img_collector_thread.r_camera_img_out -> wifi_video_data_sender_thread.r_camera_img_in;
			
			control_data_conn: port wifi_control_data_collector_thread.control_data -> control_data;	
	end controller.withThreads;

	
end StreoCamAADL;


