package StreoCamAADL
public
	with Processors::ARM;

	with Buses::I2C;

	with Buses::USB;

	with Processor_Properties;

	with Memories;

	with StereoCamAADL_Threads;

	with StereoCamAADL_Devices;

	with Buses::Ethernet;

	system vantSystem
	end vantSystem;

	system implementation vantSystem.with_devices
		subcomponents
			this_controller: process controller.withThreads;
			this_A57: processor A57.impl;
			this_camera_L: device StereoCamAADL_Devices::camera.impl;
			this_camera_R: device StereoCamAADL_Devices::camera.impl;
			this_laser: device StereoCamAADL_Devices::laser;
			this_gps: device StereoCamAADL_Devices::GPS;
			this_usb_1: bus Buses::USB::USB.impl;
			this_usb_2: bus Buses::USB::USB.impl;
			this_ethernet: bus Buses::Ethernet::Ethernet.impl;
			this_controllerLaser: process controllerLaser.withThreads;
			flash_memory: memory Memories::Flash_Memory.impl {Memory_size => 2 GByte;};
		connections
			bus_usb_1_A57: bus access this_usb_1 -> this_A57.usb_bus_1;
			bus_usb_2_A57: bus access this_usb_2 -> this_A57.usb_bus_2;
			bus_usb_3_A57: bus access this_ethernet -> this_A57.ethernet;
			bus_usb_1_cam_l: bus access this_usb_1 -> this_camera_l.usbPort;
			bus_usb_2_cam_r: bus access this_usb_2 -> this_camera_r.usbPort;
			bus_usb_3_laser: bus access this_ethernet -> this_laser.Serial_Wire;
			l_cam_on_off: port this_controller.l_camera_on_off -> this_camera_l.camera_on_off;
			r_cam_on_off: port this_controller.r_camera_on_off -> this_camera_r.camera_on_off;
			l_cam_img: port this_camera_l.imgOut -> this_controller.l_camera_img_in;
			r_cam_img: port this_camera_r.imgOut -> this_controller.r_camera_img_in;
			laser: port this_laser.proc_evt -> this_controllerLaser.proc_evt_In;
		properties
			Actual_Processor_Binding => (reference (this_A57)) applies to this_controller;
	end vantSystem.with_devices;

-----------------------
-------PROCESSOR-------
-----------------------
	processor A57 extends Processors::ARM::Generic_ARM
		features
			i2c_bus: requires bus access Buses::I2C::I2C.IMPL;
			usb_bus_1: requires bus access Buses::USB::USB.impl;
			usb_bus_2: requires bus access Buses::USB::USB.impl;
			ethernet: requires bus access Buses::Ethernet::Ethernet.impl;
		properties
			Processor_Properties::Processor_Frequency => 1430 Mhz;
			Scheduling_Protocol => (POSIX_1003_HIGHEST_PRIORITY_FIRST_PROTOCOL);
	end A57;

	processor implementation A57.impl
	end A57.impl;

-----------------------
-------PROCESS---------
-----------------------
	process controller
		features
			l_camera_img_in: in data port;
			r_camera_img_in: in data port;
			l_camera_on_off: out event port;
			r_camera_on_off: out event port;
	end controller;

	process implementation controller.withThreads
		subcomponents
			this_thread_img_processing: thread StereoCamAADL_Threads::stereo_img_collector.impl;
			this_thread_laser_processing: thread StereoCamAADL_Threads::laser_data_collector;
			this_thread_gps_processing: thread StereoCamAADL_Threads::gps_data_collector;
		connections
				--L_cam_on_off: port this_thread_img_processing.L_camera_on_off -> L_camera_on_off;
				--R_cam_on_off: port this_thread_img_processing.R_camera_on_off -> R_camera_on_off;
			l_cam_img: port l_camera_img_in -> this_thread_img_processing.l_camera_img_in;
			r_cam_img: port r_camera_img_in -> this_thread_img_processing.r_camera_img_in;
	end controller.withThreads;

	process controllerLaser
		features
			proc_evt_In: in event port;
	end controllerLaser;

	process implementation controllerLaser.withThreads
		subcomponents
			this_thread_laser_processing: thread StereoCamAADL_Threads::laser_data_collector.impl;
		connections
			Laser_sensing: port proc_evt_In -> this_thread_laser_processing.laser_trigger;
	end controllerLaser.withThreads;
	
end StreoCamAADL;


