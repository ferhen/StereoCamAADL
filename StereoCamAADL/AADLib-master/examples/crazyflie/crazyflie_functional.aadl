package Provant_Functional_Chain

-- This package models the abstract functional chain of the Crazyflie,
-- from https://wiki.bitcraze.io/doc:crazyflie:dev:fimware:sensor_to_control

public

  abstract Accelero
  features
    Accelero_Out : out data port;

  flows
    f1 : flow source Accelero_Out {latency => 100 us .. 300 us; };		

  properties
    Dispatch_Protocol => Periodic;
    Period => 4 ms; --  250 Hz
  end Accelero;
  
  abstract Sonar
  features
    Sonar_Out : out data port;

  flows
    f1 : flow source Sonar_Out {latency => 100 us .. 300 us; };		

  properties
    Dispatch_Protocol => Periodic;
    Period => 100 ms; --  250 Hz
  end Sonar;
  
   abstract GPS
  features
    GPS_Out : out data port;

  flows
    f1 : flow source GPS_Out {latency => 100 us .. 300 us; };		

  properties
    Dispatch_Protocol => Periodic;
    Period => 50 ms; --  50 Hz
  end GPS;


  abstract Gyro
  features
    Gyro_Out : out data port;
    
  flows
    f1 : flow source Gyro_Out {latency => 100 us .. 300 us; };		
    
  properties
    Dispatch_Protocol => Periodic;
    Period => 4 ms; --  250 Hz
  end Gyro;

  abstract Magneto
  features
    Magneto_Out : out data port;
    
 flows
   f1 : flow source Magneto_Out {latency => 100 us .. 300 us; };		
    
  properties
    Dispatch_Protocol => Periodic;
    Period => 4 ms; --  250 Hz
  end Magneto;

  abstract Sensor_Fusion
  features
    Sonar_In    : in data port;
    Data_Est_In : in data port;
    Accelero_In : in data port;
    Gyro_In     : in data port;
    Magneto_In  : in data port;
    Data_F_Out  : out data port; -- Fusioned data
    
  flows
    f1 : flow path Accelero_In -> Data_F_Out {latency => 100 us .. 200 us; };	
    f2 : flow path Gyro_In -> Data_F_Out {latency => 100 us .. 200 us; };
    f3 : flow path Sonar_In -> Data_F_Out {latency => 100 us .. 200 us; };
    f4 : flow path Data_Est_In -> Data_F_Out {latency => 100 us .. 200 us; };
    f5 : flow path Magneto_In -> Data_F_Out {latency => 100 us .. 200 us; };
    	   
  properties
    Dispatch_Protocol => Periodic;
    Period => 5 ms; 
    deadline               => 3 ms;
    Compute_Execution_Time => 1 ms .. 2 ms;
  end Sensor_Fusion;

abstract Position_Est
  features
    GPS_In      : in data port;
    Data_Est_Out  : out data port; -- PositionEst data
    
  flows
    f1 : flow path GPS_In -> Data_Est_Out {latency => 200 us .. 400 us; };
    	   
  properties
    Dispatch_Protocol => Periodic;
    Period => 50 ms; --  500 Hz
    deadline               => 20 ms;
    Compute_Execution_Time => 1 ms .. 2 ms;
  end Position_Est;


  abstract Controller
  	features
  	--Data_Est_In: in data port;
    Data_F_In : in data port;
    Gyro_In   : in data port;
    Motor_Out : out data port;
    TC : in data port;
  flows
  	f1 : flow path Data_F_In -> Motor_Out {latency => 100 us .. 200 us; };	
    f2 : flow path Gyro_In -> Motor_Out {latency => 100 us .. 200 us; };
    --f3 : flow path Data_Est_In -> Motor_Out {latency => 200 us .. 400 us; };
    f4 : flow path TC -> Motor_Out {latency => 100 us .. 200 us; };
    
  properties
    Dispatch_Protocol => Periodic;
    Period => 10 ms; --  500 Hz
    deadline               => 5 ms;
    Compute_Execution_Time => 1 ms .. 2 ms;
  end Controller;

  abstract Motors
  features
    Motor_In : in data port;
    
  flows
    f1 : flow sink Motor_In {latency => 100 us .. 500 us; };
    			
  properties
--    Dispatch_Protocol => Aperiodic;
    Dispatch_Protocol => Periodic;
    Period => 20ms;
  end Motors;

  abstract Pilot
  features
	Order : out data port;
  end Pilot;

  system Provant_Functional_Chain
  end Provant_Functional_Chain;

  system implementation Provant_Functional_Chain.impl
  subcomponents
    --  Sensing
    Acc        : abstract Accelero;
    Gyro       : abstract Gyro;
    Magneto    : abstract Magneto;
    Sonar	   : abstract Sonar;
    GPS        : abstract GPS;

    Pilot      : abstract Pilot;

    --  Computing
    Fusion     : abstract Sensor_Fusion;
    Controller : abstract Controller;

    --  Acting
    Motors : abstract Motors;

  connections
    C1 : port Acc.Accelero_Out     -> Fusion.Accelero_In;
    C2 : port Gyro.Gyro_Out        -> Fusion.Gyro_In;
    C3 : port Magneto.Magneto_Out  -> Fusion.Magneto_In;
    

    C4 : port Gyro.Gyro_Out        -> Controller.Gyro_In;
    C5 : port Fusion.Data_F_Out    -> Controller.Data_F_In;

    C6 : port Controller.Motor_Out -> Motors.Motor_In;

    C7 : port Pilot.Order -> Controller.TC;
    
    C8 : port Sonar.Sonar_Out -> Fusion.Sonar_In;
    C9 : port GPS.GPS_Out -> Fusion.Data_Est_In;
    
  flows
  	f_etef1 : end to end flow Gyro.f1 -> C2 -> Fusion.f2 -> C5 -> Controller.f1 -> C6 -> Motors.f1 { latency => 0 ms .. 20 ms;};
  	f_etef1bis : end to end flow Gyro.f1 -> C4 -> Controller.f2 -> C6 -> Motors.f1 { latency => 0 ms .. 2 ms;};
    f_etef2 : end to end flow Acc.f1 -> C1 -> Fusion.f1 -> C5 -> Controller.f1 -> C6 -> Motors.f1 { latency => 0 ms .. 20 ms;};
    
  end Provant_Functional_Chain.impl;

end Provant_Functional_Chain;